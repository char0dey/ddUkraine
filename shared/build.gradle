plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'com.android.library'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

kotlin {
    android()
    ios {
        binaries {
            framework {
                baseName = 'shared'
            }
        }
    }
    macosX64()
    js {
        browser()
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "io.ktor:ktor-client-core:1.6.0"
                implementation("io.ktor:ktor-client-logging:1.6.0")
                implementation("io.ktor:ktor-client-json:1.6.0")
                implementation("io.ktor:ktor-client-serialization:1.6.0")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2-native-mt"
                implementation("org.jetbrains.kotlinx:kotlinx-serialization-core:1.3.2")
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        androidMain {
            dependencies {
                api 'com.google.android.material:material:1.5.0'
                api "androidx.core:core-ktx:1.7.0"
                api 'androidx.constraintlayout:constraintlayout:2.1.3'
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2-native-mt"
                implementation "io.ktor:ktor-client-android:1.6.0"
                implementation "com.squareup.okhttp3:okhttp:4.9.2"
                implementation "io.ktor:ktor-client-logging-jvm:1.6.0"
                implementation "com.squareup.okhttp3:logging-interceptor:4.9.2"
                implementation "io.ktor:ktor-client-okhttp:1.6.0"
                implementation "io.ktor:ktor-client-apache:1.6.0"
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test-junit')
                implementation 'junit:junit:4.13'
            }
        }
        iosMain {
            dependencies {
                implementation "io.ktor:ktor-client-ios:1.6.0"
                //implementation("io.ktor:ktor-client-ios:1.6.0")
            }
        }
        iosTest {

        }
        jsMain {
            dependencies {
                implementation "io.ktor:ktor-client-js:1.6.0"
            }
        }
        macosX64Main {
            dependencies {
                implementation "io.ktor:ktor-client-core:1.6.0"
                implementation "io.ktor:ktor-client-curl:1.6.0"
            }
        }
    }
}

android {
    compileSdkVersion 31
    sourceSets["main"].manifest.srcFile("src/androidMain/AndroidManifest.xml")
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        multiDexEnabled = true
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }
}

task(packForXcode, type: Sync) {
    group = 'build'
    def mode = System.getenv('CONFIGURATION') ?: 'DEBUG'
    def sdkName = System.getenv('SDK_NAME') ?: 'iphonesimulator'
    def targetName = 'ios' + (sdkName.startsWith('iphoneos') ? 'Arm64' : 'X64')
    def framework = kotlin.targets[targetName].binaries.getFramework(mode)
    inputs.property('mode', mode)
    dependsOn(framework.linkTask)
    def targetDir = new File(buildDir, 'xcode-frameworks')
    from({ framework.outputDirectory })
    into(targetDir)
}

tasks.getByName('build').dependsOn(packForXcode)
